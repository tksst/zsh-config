# The Z Shell is copyright (c) 1992-2017 Paul Falstad, Richard Coleman,
# Zoltán Hidvégi, Andrew Main, Peter Stephenson, Sven Wischnowsky, and
# others.  All rights reserved.  Individual authors, whether or not
# specifically named, retain copyright in all changes; in what follows, they
# are referred to as `the Zsh Development Group'.  This is for convenience
# only and this body has no legal status.  The Z shell is distributed under
# the following licence; any provisions made in individual files take
# precedence.

# Permission is hereby granted, without written agreement and without
# licence or royalty fees, to use, copy, modify, and distribute this
# software and to distribute modified versions of this software for any
# purpose, provided that the above copyright notice and the following
# two paragraphs appear in all copies of this software.

# In no event shall the Zsh Development Group be liable to any party for
# direct, indirect, special, incidental, or consequential damages arising out
# of the use of this software and its documentation, even if the Zsh
# Development Group have been advised of the possibility of such damage.

# The Zsh Development Group specifically disclaim any warranties, including,
# but not limited to, the implied warranties of merchantability and fitness
# for a particular purpose.  The software provided hereunder is on an "as is"
# basis, and the Zsh Development Group have no obligation to provide
# maintenance, support, updates, enhancements, or modifications.

# tksst prompt theme

prompt_tksst_help () {
  cat <<'EOF'

  prompt tksst

EOF
}

prompt_tksst_setup () {
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' formats "%s:%b"
  zstyle ':vcs_info:*' actionformats '%s:%b|%a'

  local ts=' '

  local p_date="%F{yellow}%D{%H:%M:%S}%f$ts"
  local p_tty="%F{blue}%B%l%b%f$ts"

  local p_wsl
  [[ -n "$WSL_DISTRO_NAME" ]] && p_wsl="%F{yellow}%B$WSL_DISTRO_NAME%b%f$ts"

  local p_userhost="%K{blue}%n@%m%k$ts"
  local p_pwd="%F{green}%B%~%b%f$ts"
  local p_vcs="%(2v.%K{magenta}%2v%k$ts.)"

  local p_node="%(3v.%F{cyan}%3v%f$ts.)"

  local p_shlvl="%(2L.%F{magenta}zsh/$SHLVL%f$ts.)"
  local p_rc="%(?..%F{red}%B%?%1v%b%f )"
  local p_end="%B%#%b "


  typeset -ga zle_highlight
  zle_highlight[(r)default:*]=default:cyan

  prompt="$p_rc$p_date$p_tty$p_shlvl$p_wsl$p_userhost$p_pwd$p_vcs$p_node
$p_end"
  PS2='%(4_.\.)%3_> %E'

  add-zsh-hook precmd prompt_tksst_precmd
}

prompt_tksst_precmd () {
  setopt noxtrace noksharrays localoptions
  local exitstatus=$?

  psvar=()
  [[ $exitstatus -ge 128 ]] && psvar[1]=" $signals[$exitstatus-127]" ||
	psvar[1]=""

  [[ -o interactive ]] && jobs -l

  vcs_info
  [[ -n $vcs_info_msg_0_ ]] && psvar[2]="$vcs_info_msg_0_"

  if [[ -n $NVM_BIN && -e package.json ]]; then
    psvar[3]="node(nvm):${${NVM_BIN##*/versions/node/}%/bin}"
  fi
}

prompt_tksst_setup "$@"
